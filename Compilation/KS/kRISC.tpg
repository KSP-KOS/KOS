<% @TinyPG Namespace="kOS.Compilation.KS" Language="C#"%>

// Terminals
// ===================================================
//Math
PLUSMINUS -> @"(\+|-)";
MULT -> @"\*";
DIV -> @"/";
POWER -> @"\^";
E -> @"e";
//Logic
AND -> @"and";
OR -> @"or";
TRUEFALSE -> @"true|false";
COMPARATOR -> @">=|<=|=|>|<";
//Instructions tokens
SET -> @"set";
TO -> @"to";
IF -> @"if";
UNTIL -> @"until";
LOCK -> @"lock";
UNLOCK -> @"unlock";
PRINT -> @"print";
AT -> @"at";
ON -> @"on";
TOGGLE -> @"toggle";
WAIT -> @"wait";
WHEN -> @"when";
THEN -> @"then";
OFF -> @"off";
STAGE -> @"stage";
CLEARSCREEN -> @"clearscreen";
ADD -> @"add";
REMOVE -> @"remove";
LOG -> @"log";
BREAK -> @"break";
DECLARE -> @"declare";
PARAMETER -> @"parameter";
SWITCH -> @"switch";
COPY -> @"copy";
FROM -> @"from";
RENAME -> @"rename";
VOLUME -> @"volume";
FILE -> @"file";
DELETE -> @"delete";
EDIT -> @"edit";
RUN -> @"run";
LIST -> @"list";
REBOOT -> @"reboot";
SHUTDOWN -> @"shutdown";
FOR -> @"for";
UNSET -> @"unset";
BATCH -> @"batch";
DEPLOY -> @"deploy";
//Generic
BRACKETOPEN -> @"\(";
BRACKETCLOSE -> @"\)";
CURLYOPEN -> @"\{";
CURLYCLOSE -> @"\}";
COMMA -> @",";
COLON -> @":";
IN -> @"in";
ARRAYINDEX -> @"#";
ALL -> @"all";
IDENTIFIER -> @"[a-z_][a-z0-9_]*";
INTEGER -> @"[0-9]+";
DOUBLE -> @"[0-9]*\.[0-9]+";
STRING -> @"@?\""(\""\""|[^\""])*\""";
EOI -> @"\.";
//Special
EOF -> @"^$";
[Skip]
WHITESPACE -> @"\s+";
[Skip]
COMMENTLINE -> @"//[^\n]*\n?";

// Rules
// ===================================================
Start -> (instruction)* EOF;
instruction_block -> instruction | CURLYOPEN instruction+ CURLYCLOSE EOI?;
instruction -> set_stmt | if_stmt | until_stmt | lock_stmt | unlock_stmt | print_stmt |
               on_stmt | toggle_stmt | wait_stmt | when_stmt | onoff_stmt | stage_stmt |
               clear_stmt | add_stmt | remove_stmt | log_stmt | break_stmt | declare_stmt |
               switch_stmt | copy_stmt | rename_stmt | delete_stmt | edit_stmt | run_stmt |
               list_stmt | reboot_stmt | shutdown_stmt | for_stmt | unset_stmt | batch_stmt |
               deploy_stmt;
// statements
set_stmt -> SET varidentifier TO expr EOI;
if_stmt -> IF expr instruction_block EOI?;
until_stmt -> UNTIL expr instruction_block EOI?;
lock_stmt -> LOCK IDENTIFIER TO expr EOI;
unlock_stmt -> UNLOCK IDENTIFIER EOI;
print_stmt -> PRINT expr (AT BRACKETOPEN INTEGER COMMA INTEGER BRACKETCLOSE)? EOI;
on_stmt -> ON varidentifier instruction_block EOI?;
toggle_stmt -> TOGGLE varidentifier EOI;
wait_stmt -> WAIT UNTIL? expr EOI;
when_stmt -> WHEN expr THEN instruction_block EOI?;
onoff_stmt -> varidentifier (ON | OFF) EOI;
stage_stmt -> STAGE EOI;
clear_stmt -> CLEARSCREEN EOI;
add_stmt -> ADD expr EOI;
remove_stmt -> REMOVE expr EOI;
log_stmt -> LOG expr TO IDENTIFIER EOI;
break_stmt -> BREAK EOI;
declare_stmt -> DECLARE PARAMETER? IDENTIFIER (COMMA IDENTIFIER)* EOI;
switch_stmt -> SWITCH TO filevol_name EOI;
copy_stmt -> COPY IDENTIFIER (FROM | TO) filevol_name EOI;
rename_stmt -> RENAME (VOLUME | FILE)? filevol_name TO IDENTIFIER EOI;
delete_stmt -> DELETE IDENTIFIER (FROM filevol_name)? EOI;
edit_stmt -> EDIT IDENTIFIER EOI;
run_stmt -> RUN IDENTIFIER (BRACKETOPEN arglist BRACKETCLOSE)? (ON filevol_name)? EOI;
filevol_name -> INTEGER | IDENTIFIER;
list_stmt -> LIST (IDENTIFIER (IN IDENTIFIER)?)? EOI;
reboot_stmt -> REBOOT EOI;
shutdown_stmt -> SHUTDOWN EOI;
for_stmt -> FOR IDENTIFIER IN varidentifier instruction_block EOI?;
unset_stmt -> UNSET (IDENTIFIER | ALL) EOI;
batch_stmt -> BATCH EOI;
deploy_stmt -> DEPLOY EOI;
// expressions
arglist -> expr (COMMA expr)*;
// ------ for github #20, changed this:
//expr -> or_expr (COMPARATOR or_expr)*;
//or_expr -> and_expr (OR and_expr)*;
//and_expr -> arith_expr (AND arith_expr)*;
// ------- into this:
expr -> and_expr (OR and_expr)*;
and_expr -> compare_expr (AND compare_expr)*;
compare_expr -> arith_expr (COMPARATOR arith_expr)*;
// -------
arith_expr -> div_expr (PLUSMINUS div_expr)*;
div_expr -> mult_expr (DIV mult_expr)*;
mult_expr -> factor (MULT factor)*;
factor -> atom (POWER atom)*;
atom -> PLUSMINUS? ( sci_number | 
                     TRUEFALSE |
                     varidentifier |
                     BRACKETOPEN expr BRACKETCLOSE ) |
        STRING;
sci_number -> number (E PLUSMINUS? INTEGER)?;
number -> (INTEGER | DOUBLE);
varidentifier -> array_identifier (COLON IDENTIFIER)*;
array_identifier -> function_identifier (ARRAYINDEX (INTEGER | IDENTIFIER))*;
function_identifier -> IDENTIFIER (BRACKETOPEN arglist? BRACKETCLOSE)?;